# Chmouel Boudjnah <chmouel@chmouel.com>  -*- mode: shell-script; -*-

install_packages() {
    local packages=(
        agkozak/zsh-z
        zsh-users/zsh-autosuggestions
        trystan2k/zsh-tab-title
        chmouel/chmoujump
        b4b4r07/emoji-cli::emoji-cli.zsh
    )
    local base_url=https://github.com
    local cachedir=${cachedir:-$HOME/.cache/zsh}
    cachedir=${cachedir}/repos
    local p
    for p in ${packages[@]};do
        local fname=""
        [[ ${p} == *::* ]] && {
            fname=${p#*::}
            p=${p%::*}
        }
        local dirname=${p%%/*}
        local basename=${p##*/}
        fname=${fname:-${basename}.plugin.zsh}
        local plugin=${cachedir}/${p}/${fname}
        local url=${base_url}/${p}
        
        [[ -d ${cachedir}/${p} ]] || {
            echo -e "plugin clone from ${url}"
            mkdir -p ${cachedir}/${dirname}
            git clone --depth=1 ${url} ${cachedir}/${p}
        }
        [[ -e ${plugin} ]] && source ${plugin}
    done
}

install_packages

# vim: ft=zsh
