#!/usr/bin/zsh
# Chmouel Boudjnah <chmouel@chmouel.com>

confdir=$HOME/.shell
basehost=${HOST%%.*}
[[ $1 == 'reloading' ]] && reload=yes

autoload -U colors;colors

# We need that first
setopt nonomatch

#Local functions we want to autoload
typeset -U fpath
user_fun_path=($confdir/functions $confdir/functions/hosts/${basehost});
fpath=($user_fun_path $fpath); autoload -U compinit
for func ($user_fun_path) autoload -U $func/*(x:t)
for func ($user_fun_path/*) [[ ${func} == *.source ]] && source ${func}

#get environement and alias
[[ -f $confdir/config/environement ]] && source $confdir/config/environement
[[ -f $confdir/config/alias ]] && source $confdir/config/alias
[[ -f $HOME/.shell/hosts/$basehost.sh ]] && source $HOME/.shell/hosts/$basehost.sh

#Compinit
[[ -z $reload ]] && compinit -i

#PS1
if [[ -z $CUSTOM_PROMPT ]];then
    [[ -z $hostColor ]] && hostColor=white
    [[ -z $userColor ]] && userColor=yellow
    [[ -z $restColor ]] && restColor=cyan
    PS1="%{$fg_bold[$userColor]%}%n%{$fg_no_bold[white]%}@%{$fg_bold[$hostColor]%}%m%{$fg_no_bold[white]%}:%{$fg_bold[$restColor]%}%3~$%{$fg_no_bold[white]%} "
fi

#Zsh: Alias
alias -g L="|less"
alias -g MJ="| python -mjson.tool"
alias -g EEL=' 2>&1 | less'
alias so="source $HOME/.zshrc reloading && unset reload"
alias o='popd'

#Considers only alphanumeric as work like in bash
autoload -U select-word-style
select-word-style bash


# Path
typeset -U path
path=($HOME/bin /usr/local/bin /usr/local/sbin /usr/sbin /sbin $path)

#History Settings
HISTFILE="$HOME/.zsh_history"
HISTSIZE="5000000"
SAVEHIST="5000000"
export HISTSIZE HISTSIZE SAVEHIST

setopt extended_history hist_ignore_all_dups \
    append_history hist_ignore_dups hist_ignore_space \
    hist_reduce_blanks hist_save_no_dups histverify nohistbeep \

type -p on_ac_power &>/dev/null && on_ac_power && setopt inc_append_history

#setopt
setopt nobeep AUTO_LIST COMPLETE_IN_WORD CORRECT EXTENDED_GLOB \
    AUTO_CD noAUTO_MENU noFLOW_CONTROL AUTO_PUSHD completeinword \
    interactivecomments nopromptcr alwaystoend

#Ignore stuff starting by _ for the completion.
export CORRECT_IGNORE="_*"

#cache completion
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path $HOME/.shell/cache

#Completion style
zstyle ':completion:*' special-dirs true

# Ignore completion functions for commands you donâ€™t have
zstyle ':completion:*:functions' ignored-patterns '_*'

#Auto quote URL
#autoload -U url-quote-magic
#zle -N self-insert url-quote-magic

#Keys
stty -ixon #disable c-q c-s
bindkey -e
bindkey -s '^Xd' "\`date '+%Y%m%d'\`\t" #`"
bindkey -s "^Xs" "^p^asudo ^e"
# M-# comment current line
bindkey '^[#' pound-insert

#Ala emacs
zmodload -i zsh/deltochar
bindkey -e "^[z" zap-to-char

source $confdir/config/termsupport.zsh


_dircolors=$(whence dircolors) 
[[ -z ${_dircolors} ]] && _dircolors=$(whence gdircolors)
#LS Colors in Completion
[[ -n ${_dircolors} &&  -z $LS_COLORS ]] && eval $(${_dircolors} -b) && \
    export LS_COLOR="${LS_COLORS}:*.pyc=01;30" && \
    zstyle ":completion:*" list-colors ${(s.:.)LS_COLORS}

unset _dircolors

#on ssh-copy-id compile user-at-host
compdef _user_at_host ssh-copy-id

[[ -f $HOME/.shell/hosts/${basehost}-post.sh ]] && source $HOME/.shell/hosts/${basehost}-post.sh

#Unsetting
unset basehost confdir func user_fun_path reload hostColor userColor restColor


if [[ "$TERM" = "dumb" ]];then
  unsetopt zle
  unsetopt prompt_cr
  unsetopt prompt_subst
  unfunction precmd
  unfunction preexec
  PS1='$ '
fi
