#!/usr/bin/zsh
# Chmouel Boudjnah <chmouel@chmouel.com>

confdir=$HOME/.shell/
basehost=${HOST%%.*}
[[ $1 == 'reloading' ]] && reload=yes

autoload -U colors;colors

#Local functions we want to autoload
typeset -U fpath
user_fun_path=($confdir/functions);
fpath=($user_fun_path $fpath); autoload -U compinit
for func ($user_fun_path) autoload -U $func/*(.x:t)

#get environement and alias
[[ -f $confdir/config/environement ]] && source $confdir/config/environement
[[ -f $confdir/config/alias ]] && source $confdir/config/alias
[[ -f $HOME/.shell/hosts/$basehost.sh ]] && source $HOME/.shell/hosts/$basehost.sh

#Compinit
[[ -z $reload ]] && compinit -i

#PS1
if [[ -z $custom_prompt ]];then
    [[ -z $hostColor ]] && hostColor=white
    [[ -z $userColor ]] && userColor=yellow
    [[ -z $restColor ]] && restColor=cyan
    PS1="%{$fg_bold[$userColor]%}%n%{$fg_no_bold[white]%}@%{$fg_bold[$hostColor]%}%m%{$fg_no_bold[white]%}:%{$fg_bold[$restColor]%}%3~$%{$fg_no_bold[white]%} "
fi
    
#Zsh: Alias 
alias -g L="|less"
alias so="source $HOME/.zshrc reloading && unset reload"
alias o='popd'

#Variables
WORDCHARS=''

# Path
typeset -U path
path=(~/bin /usr/sbin /sbin $path)

#History Settings
HISTFILE="$HOME/.bash_history"
HISTSIZE="9000000"
SAVEHIST="9000000"
export HISTSIZE HISTSIZE SAVEHIST

setopt extended_history hist_ignore_all_dups \
    append_history hist_ignore_dups hist_ignore_space \
    hist_reduce_blanks hist_save_no_dups histverify nohistbeep \
    
[[ -e /usr/bin/on_ac_power ]] && on_ac_power && setopt inc_append_history

#setopt
setopt nobeep AUTO_LIST COMPLETE_IN_WORD CORRECT EXTENDED_GLOB \
    AUTO_CD noAUTO_MENU noFLOW_CONTROL AUTO_PUSHD completeinword \
    interactivecomments nopromptcr alwaystoend

export CORRECT_IGNORE="_*"

#User-Hosts Completions
hosts=(${${${${(f)"$(<$HOME/.ssh/known_hosts)"}:#[^a-zA-Z0-9]*}%%\ *}%%,*})
[[ -n $hosts ]] && zstyle ':completion:*' hosts $hosts

#cache completion
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.shell/cache

#Kill
zstyle ':completion:*:*:kill:*:processes' list-colors "=(#b) #([0-9]#)*=$color[cyan]=$color[red]"
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:kill:*' force-list always
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'
zstyle ':completion:*:kill:*' insert-ids single
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:kill:*' force-list always
zstyle ':completion:*:processes' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'

#Auto quote URL
autoload -U url-quote-magic
zle -N self-insert url-quote-magic

#Cache
zstyle ':completion:*' use-cache on

#Keys
stty -ixon #disable c-q c-s
bindkey -e
bindkey '\C-w' kill-region
bindkey '^i' expand-or-complete-prefix
bindkey -e '^[[5D' backward-word
bindkey -e '^[[5C' forward-word
bindkey -s '^Xd' "\`date '+%Y%m%d'\`\t" #`"

#Ala emacs
zmodload -i zsh/deltochar
bindkey -e "^[z" zap-to-char

#TITLES
[[ $TERM == "screen" ]] && print -nR $'\033k'${(U)HOST%%.*}$'\033'\\

if [[ $TERM == xterm* ]];then
    print -nR $'\033]0;'${(U)HOST%%.*}$'\a'
fi

#LS Colors in Completion
zstyle ":completion:*" list-colors ${(s.:.)LS_COLORS}

#on ssh-copy-id compile user-at-host
compdef _user_at_host ssh-copy-id

#Unsetting
unset basehost confdir func user_fun_path reload hostColor userColor restColor
unfunction make-it-like-bash \
				2>/dev/null >/dev/null || :

if [[ "$TERM" = "dumb" ]];then
  unsetopt zle
  unsetopt prompt_cr
  unsetopt prompt_subst
  unfunction precmd
  unfunction preexec
  PS1='$ '
fi

type -p lessfile &>/dev/null  && eval $(lessfile)
umask 022
