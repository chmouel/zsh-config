#!/usr/bin/env zsh
# Chmouel Boudjnah <chmouel@chmouel.com>  -*- mode: shell-script; -*-
#
# very tiny zsh plugin manager that support only manage repos and source
# plugins
#
# need a repo array of owner/repo, only GH public is supported atm
# i.e:
# ZSH_PLUGINS=(
#         chmouel/chmoujump # -> https://github.com/chmouel/chmoujump
#         foo/bar::entrypoint.zsh # source entrypoint.zsh instead of the conventional plugins_name.plugins.zsh
# )
#
# use -u to update the repos
# use -l to list your plugins

_vtplug() {
    zmodload zsh/zutil
    local p update prestep

    zparseopts -D -E -F - p=prestep l=list u=update || exit 1
    local end_opts=$@[(i)(--|-)]
    set -- "${@[0,end_opts-1]}" "${@[end_opts+1,-1]}"
    local base_url=https://github.com
    local cachedir=${cachedir:-$HOME/.cache/zsh}
    cachedir=${cachedir}/repos
    [[ -n ${list} ]] && {
        print -rC1 -- ${ZSH_PLUGINS[@]}
        return 0
    }
    for p in ${ZSH_PLUGINS[@]};do
        local fname=""
        [[ ${p} == *::* ]] && {
            fname=${p#*::}
            p=${p%::*}
        }
        local dirname=${p%%/*}
        local basename=${p##*/}
        fname=${fname:-${basename}.plugin.zsh}
        local plugin=${cachedir}/${p}/${fname}
        local url=${base_url}/${p}

        [[ -n ${prestep} ]] && {
            fpath+=${cachedir}/${p}
            continue
        }

        if [[ ! -d ${cachedir}/${p} ]];then
            echo -e "plugin clone: ${url}"
            mkdir -p ${cachedir}/${dirname}
            git clone --depth=1 ${url} ${cachedir}/${p}
        elif [[ -n ${update} ]];then
            (
                echo "plugin update: ${url}"
                pushd ${cachedir}/${p} >/dev/null &&
                    git pull -q
                popd >/dev/null
            )
        fi

        fpath+=${cachedir}/${p}
        [[ -e ${plugin} ]] && source ${plugin}
    done
}

_vtplug $@

# vim: ft=zsh
